--https://msdn.microsoft.com/en-us/library/ms181700.aspx

USE master;
GO

DROP LOGIN BobSmith;
GO

IF DATABASEPROPERTYEX('AAA', 'Status') IS NOT NULL
BEGIN
	ALTER DATABASE AAA SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
	DROP DATABASE AAA;
END
GO

CREATE DATABASE AAA
ON PRIMARY (
    NAME='AAAData',
    FILENAME='H:\_DATA\AAA.MDF',
    SIZE = 250MB
)
LOG ON (
    NAME = 'AAALog',
    FILENAME = 'C:\_LOG\AAA.LDF',
    SIZE = 400MB,
    FILEGROWTH=100MB
);
GO

USE AAA;
GO

CREATE TABLE DataTable ( Value INT IDENTITY );
GO

INSERT DataTable DEFAULT VALUES;
GO

CREATE USER JohnDoe WITHOUT LOGIN;
GO

ALTER AUTHORIZATION ON DataTable TO JohnDoe;
GO

CREATE CERTIFICATE CoreCertificate
ENCRYPTION BY PASSWORD = 'taco'
WITH SUBJECT = 'First cert';

CREATE USER JaneSmith
FROM CERTIFICATE CoreCertificate;

GRANT SELECT ON DataTable TO JaneSmith;
GO

CREATE LOGIN BobSmith WITH PASSWORD = 'burrito';

CREATE USER BobSmith;
GO

--+ NO ACCESS
EXECUTE AS LOGIN = 'BobSmith';
	SELECT * FROM DataTable;
REVERT;
GO

CREATE PROCEDURE CoreProc
AS
BEGIN
	PRINT 'CoreProc';
	SELECT * FROM DataTable;
END;
GO

GRANT EXECUTE ON CoreProc TO public;
GO

CREATE PROCEDURE CoreProc_BobSmith
AS
BEGIN
	IF USER_ID() != USER_ID('BobSmith')
	BEGIN
		PRINT 'ACCESS DENIED: BobSmith only.'
		RETURN
	END
	EXEC CoreProc;
END;
GO

GRANT EXECUTE ON CoreProc_BobSmith TO public;
GO

EXEC CoreProc_BobSmith;

--+ NO ACCESS
EXECUTE AS LOGIN = 'BobSmith';
	EXEC CoreProc_BobSmith
REVERT;
GO

ADD SIGNATURE TO CoreProc_BobSmith
BY CERTIFICATE CoreCertificate
WITH PASSWORD = 'taco';

ADD COUNTER SIGNATURE TO CoreProc
BY CERTIFICATE CoreCertificate
WITH PASSWORD = 'taco';

--+ CoreProc_BobSmith WORKS
--+ CoreProc ACCESS DENIED
EXECUTE AS LOGIN = 'BobSmith';
	EXEC CoreProc_BobSmith
	EXEC CoreProc
REVERT;
GO

USE master;
GO

IF DATABASEPROPERTYEX('AAA', 'Status') IS NOT NULL
BEGIN
	ALTER DATABASE AAA SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
	DROP DATABASE AAA;
END
GO

DROP LOGIN BobSmith;
GO