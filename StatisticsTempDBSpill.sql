USE master;
GO

IF DATABASEPROPERTYEX('MemoryGrantExample', 'Status') IS NOT NULL
BEGIN
	ALTER DATABASE MemoryGrantExample SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
	DROP DATABASE MemoryGrantExample;
END
GO

CREATE DATABASE MemoryGrantExample;
GO

USE MemoryGrantExample;
GO

DBCC TRACEON (3604);
GO

CREATE TABLE DataTable
(
ID INT NOT NULL,
Col INT NOT NULL,
Placeholder CHAR(8000)
);
GO

CREATE UNIQUE CLUSTERED INDEX CI_DataTable_ID ON DataTable(ID);
GO

WITH N1(C) AS (SELECT 0 UNION ALL SELECT 0) 
,N2(C) AS (SELECT 0 FROM N1 AS T1 CROSS JOIN N1 AS T2) 
,N3(C) AS (SELECT 0 FROM N2 AS T1 CROSS JOIN N2 AS T2) 
,N4(C) AS (SELECT 0 FROM N3 AS T1 CROSS JOIN N3 AS T2) 
,N5(C) AS (SELECT 0 FROM N4 AS T1 CROSS JOIN N4 AS T2) 
,IDS(ID) AS (SELECT ROW_NUMBER() OVER (ORDER BY (SELECT NULL)) FROM N5)
INSERT INTO DataTable(ID, Col, Placeholder)
SELECT ID, ID % 100, CONVERT(CHAR(100),ID)
FROM IDs;
GO

CREATE NONCLUSTERED INDEX NCI_DataTable_Col ON DataTable(Col);
GO

DBCC SHOW_STATISTICS('DataTable', 'NCI_DataTable_Col')
GO

;WITH N1(C) AS (SELECT 0 UNION ALL SELECT 0) 
,N2(C) AS (SELECT 0 FROM N1 AS T1 CROSS JOIN N1 AS T2)
,N3(C) AS (SELECT 0 FROM N2 AS T1 CROSS JOIN N2 AS T2)
,N4(C) AS (SELECT 0 FROM N3 AS T1 CROSS JOIN N3 AS T2)
,N5(C) AS (SELECT 0 FROM N4 AS T1 CROSS JOIN N2 AS T2)
,IDS(ID) AS (SELECT ROW_NUMBER() OVER (ORDER BY (SELECT NULL)) FROM N5)
INSERT INTO DataTable(ID, Col, Placeholder)
SELECT 100000 + ID, 1000, CONVERT(char(100),ID)
FROM IDs
WHERE ID <= 656;
GO

DECLARE @Dummy INT
SET STATISTICS TIME ON
SELECT @Dummy = ID FROM DataTable WHERE Col = 1 ORDER BY Placeholder

-- spills to tempdb
SELECT @Dummy = ID FROM DataTable WHERE Col = 1000 ORDER BY Placeholder
SET STATISTICS TIME OFF
GO

DBCC SHOW_STATISTICS('DataTable', 'NCI_DataTable_Col')
GO

UPDATE STATISTICS DataTable NCI_DataTable_Col WITH FULLSCAN
GO

USE master;
GO

IF DATABASEPROPERTYEX('MemoryGrantExample', 'Status') IS NOT NULL
BEGIN
	ALTER DATABASE MemoryGrantExample SET SINGLE_USER WITH ROLLBACK IMMEDIATE;
	DROP DATABASE MemoryGrantExample;
END
GO